Elixir has three error mechanisms: errors, throws, and exits

# Errors
Errors (or exceptions) are used when exceptional things happen in the code

    iex> :foo + 1
    ** (ArithmeticError) bad argument in arithmetic expression
        :erlang.+(:foo, 1)
        
A runtime error can be raised

    iex> raise "oops"
    ** (RuntimeError) oops
    
    # passing the error name and a list of keyword arguments:
    iex> raise ArgumentError, message: "invalid argument foo"
    ** (ArgumentError) invalid argument foo
    
define your own errors by creating a module and using the defexception construct inside it

    iex> defmodule MyError do
    iex>   defexception message: "default message"
    iex> end
    iex> raise MyError
    ** (MyError) default message
    iex> raise MyError, message: "custom message"
    ** (MyError) custom message

Errors can be rescued using the try/rescue construct

    iex> try do
    ...>   raise "oops"
    ...> rescue
    ...>   e in RuntimeError -> e
    ...> end
    %RuntimeError{message: "oops"}
rescues the runtime error and returns the error itself

In practice, however, Elixir developers rarely use the try/rescue construct. 
Elixir instead provides a File.read/1 function which returns a tuple containing information about whether the file was opened successfully:

    # There is no try/rescue here
    iex> File.read("hello")
    {:error, :enoent}
    iex> File.write("hello", "world")
    :ok
    iex> File.read("hello")
    {:ok, "world"}

In case you want to handle multiple outcomes of opening a file, you can use pattern matching within the case construct:

    iex> case File.read("hello") do
    ...>   {:ok, body} -> IO.puts("Success: #{body}")
    ...>   {:error, reason} -> IO.puts("Error: #{reason}")
    ...> end

Use !, raises an exception if there’s an error. 

    iex> File.read!("unknown")
    ** (File.Error) could not read file unknown: no such file or directory
        (elixir) lib/file.ex:272: File.read!/1

# Throws
a value can be thrown and later be caught. throw and catch are reserved for situations where it is not possible to retrieve a value unless by using throw and catch.
Those situations are quite uncommon in practice except when interfacing with libraries that do not provide a proper API.

    iex> try do
    ...>   Enum.each(-50..50, fn(x) ->
    ...>     if rem(x, 13) == 0, do: throw(x)
    ...>   end)
    ...>   "Got nothing"
    ...> catch
    ...>   x -> "Got #{x}"
    ...> end
    "Got -39"
    
# Exits
All Elixir code runs inside processes that communicate with each other. When a process dies of “natural causes” (e.g., unhandled exceptions), it sends an exit signal. A process can also die by explicitly sending an exit signal

    iex> spawn_link(fn -> exit(1) end)
    ** (EXIT from #PID<0.56.0>) evaluator process exited with reason: 1
    
# After
Sometimes it’s necessary to ensure that a resource is cleaned up after some action that could potentially raise an error. The try/after construct allows you to do that. 

    iex> {:ok, file} = File.open("sample", [:utf8, :write])
    iex> try do
    ...>   IO.write(file, "olá")
    ...>   raise "oops, something went wrong"
    ...> after
    ...>   File.close(file)
    ...> end
    ** (RuntimeError) oops, something went wrong
The after clause will be executed regardless of whether or not the tried block succeeds.

Sometimes you may want to wrap the entire body of a function in a try construct, often to guarantee some code will be executed afterwards. In such cases, Elixir allows you to omit the try line:

    iex> defmodule RunAfter do
    ...>   def without_even_trying do
    ...>     raise "oops"
    ...>   after
    ...>     IO.puts "cleaning up!"
    ...>   end
    ...> end
    iex> RunAfter.without_even_trying
    cleaning up!
    ** (RuntimeError) oops
Elixir will automatically wrap the function body in a try whenever one of after, rescue or catch is specified.

# Else
If an else block is present, it will match on the results of the try block whenever the try block finishes without a throw or an error.

    iex> x = 2
    2
    iex> try do
    ...>   1 / x
    ...> rescue
    ...>   ArithmeticError ->
    ...>     :infinity
    ...> else
    ...>   y when y < 1 and y > -1 ->
    ...>     :small
    ...>   _ ->
    ...>     :large
    ...> end
    :small
    
# Variables scope
It is important to bear in mind that variables defined inside try/catch/rescue/after blocks do not leak to the outer context. 

    iex> try do
    ...>   raise "fail"
    ...>   what_happened = :did_not_raise
    ...> rescue
    ...>   _ -> what_happened = :rescued
    ...> end
    iex> what_happened
    ** (RuntimeError) undefined function: what_happened/0
    
Instead, you can store the value of the try expression:

    iex> what_happened =
    ...>   try do
    ...>     raise "fail"
    ...>     :did_not_raise
    ...>   rescue
    ...>     _ -> :rescued
    ...>   end
    iex> what_happened
    :rescued
