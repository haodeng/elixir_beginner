# Enumerables and Streams
## Enumerables
The Enum module provides a huge range of functions to transform, sort, group, filter and retrieve items from enumerables. 
It is one of the modules developers use frequently in their Elixir code.

    iex> Enum.map([1, 2, 3], fn x -> x * 2 end)
    [2, 4, 6]
    iex> Enum.map(%{1 => 2, 3 => 4}, fn {k, v} -> k * v end)
    [2, 12]
    
    # Elixir also provides ranges
    iex> Enum.map(1..3, fn x -> x * 2 end)
    [2, 4, 6]
    iex> Enum.reduce(1..3, 0, &+/2)
    6

All the functions in the Enum module are eager

    iex> odd? = &(rem(&1, 2) != 0)
    #Function<6.80484245/1 in :erl_eval.expr/5>
    iex> Enum.filter(1..3, odd?)
    [1, 3]
This means that when performing multiple operations with Enum, each operation is going to generate an intermediate list until we reach the result:

    # We start with a range and then multiply each element in the range by 3. 
    # This first operation will now create and return a list with 100_000 items. 
    # Then we keep all odd elements from the list, generating a new list, now with 50_000 items, 
    # and then we sum all entries.
    iex> 1..100_000 |> Enum.map(&(&1 * 3)) |> Enum.filter(odd?) |> Enum.sum()
    7500000000
The |> symbol used in the snippet above is the pipe operator. 
To see how it can make the code cleaner, have a look at the example above rewritten without using the |> operator:

    iex> Enum.sum(Enum.filter(Enum.map(1..100_000, &(&1 * 3)), odd?))
    7500000000
    
## Streams
As an alternative to Enum, Elixir provides the Stream module which supports lazy operations:

    iex> 1..100_000 |> Stream.map(&(&1 * 3)) |> Stream.filter(odd?) |> Enum.sum
    7500000000

Streams are lazy, composable enumerables
Instead of generating intermediate lists, streams build a series of computations that are invoked only when we pass the underlying stream to the Enum module. 
Streams are useful when working with large, possibly infinite, collections.

    # Stream.cycle/1 can be used to create a stream that cycles a given enumerable infinitely. 
    # Be careful to not call a function like Enum.map/2 on such streams, as they would cycle forever:
    iex> stream = Stream.cycle([1, 2, 3])
    #Function<15.16982430/2 in Stream.unfold/2>
    iex> Enum.take(stream, 10)
    [1, 2, 3, 1, 2, 3, 1, 2, 3, 1]
    
    # Stream.unfold/2 can be used to generate values from a given initial value:
    iex> stream = Stream.unfold("hełło", &String.next_codepoint/1)
    #Function<39.75994740/2 in Stream.unfold/2>
    iex> Enum.take(stream, 3)
    ["h", "e", "ł"]
    
    # Stream.resource/3 which can be used to wrap around resources, 
    # guaranteeing they are opened right before enumeration and closed afterwards, even in the case of failures. 
    # For example, File.stream!/1 builds on top of Stream.resource/3 to stream files:
    iex> stream = File.stream!("path/to/file")
    %File.Stream{
      line_or_bytes: :line,
      modes: [:raw, :read_ahead, :binary],
      path: "path/to/file",
      raw: true
    }
    iex> Enum.take(stream, 10)
    
The example above will fetch the first 10 lines of the file you have selected. 
This means streams can be very useful for handling large files or even slow resources like network resources.


In particular, focus on the Enum module first and only move to Stream for the particular scenarios where laziness is required, 
to either deal with slow resources or large, possibly infinite, collections.
