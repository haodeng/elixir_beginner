# Keyword lists
In Elixir, when we have a list of tuples and the first item of the tuple (i.e. the key) is an atom, we call it a keyword list

    iex> list = [{:a, 1}, {:b, 2}]
    [a: 1, b: 2]
    # Elixir supports a special syntax for defining such lists: [key: value]
    iex> list == [a: 1, b: 2]
    true

Since keyword lists are lists, we can use all operations available to lists. 

    iex> list ++ [c: 3]
    [a: 1, b: 2, c: 3]
    iex> [a: 0] ++ list
    [a: 0, a: 1, b: 2]
Note that values added to the front are the ones fetched on lookup

    iex> new_list = [a: 0] ++ list
    [a: 0, a: 1, b: 2]
    iex> new_list[:a]
    0

Keyword lists are important because they have three special characteristics:

* Keys must be atoms.
* Keys are ordered, as specified by the developer.
* Keys can be given more than once.

# Maps
Whenever you need a key-value store, maps are the “go to” data structure in Elixir. A map is created using the %{} syntax:

    iex> map = %{:a => 1, 2 => :b}
    %{2 => :b, :a => 1}
    iex> map[:a]
    1
    iex> map[2]
    :b
    iex> map[:c]
    nil
Compared to keyword lists, we can already see two differences:

* Maps allow any value as a key.
* Maps’ keys do not follow any ordering.

In contrast to keyword lists, maps are very useful with pattern matching. When a map is used in a pattern, it will always match on a subset of the given value:

    #  an empty map matches all maps
    iex> %{} = %{:a => 1, 2 => :b}
    %{2 => :b, :a => 1}
    iex> %{:a => a} = %{:a => 1, 2 => :b}
    %{2 => :b, :a => 1}
    iex> a
    1
    # a map matches as long as the keys in the pattern exist in the given map

Variables can be used when accessing, matching and adding map keys:

    iex> n = 1
    1
    iex> map = %{n => :one}
    %{1 => :one}
    iex> map[n]
    :one
    iex> %{^n => :one} = %{1 => :one, 2 => :two, 3 => :three}
    %{1 => :one, 2 => :two, 3 => :three}
    iex> %{:c => c} = %{:a => 1, 2 => :b}
    ** (MatchError) no match of right hand side value: %{2 => :b, :a => 1}
    
The Map module provides a very similar API to the Keyword module with convenience functions to manipulate maps

    iex> Map.get(%{:a => 1, 2 => :b}, :a)
    1
    iex> Map.put(%{:a => 1, 2 => :b}, :c, 3)
    %{2 => :b, :a => 1, :c => 3}
    iex> Map.to_list(%{:a => 1, 2 => :b})
    [{2, :b}, {:a, 1}]
Maps have the following syntax for updating a key’s value:

    iex> map = %{:a => 1, 2 => :b}
    %{2 => :b, :a => 1}

    iex> %{map | 2 => "two"}
    %{2 => "two", :a => 1}
    # The syntax requires the given key to exist. It cannot be used to add new keys. 
    iex> %{map | :c => 3}
    ** (KeyError) key :c not found in: %{2 => :b, :a => 1}

When all the keys in a map are atoms, you can use the keyword syntax for convenience:

    iex> map = %{a: 1, b: 2}
    %{a: 1, b: 2}

Another interesting property of maps is that they provide their own syntax for accessing atom keys:

    iex> map = %{:a => 1, 2 => :b}
    %{2 => :b, :a => 1}

    iex> map.a
    1
    iex> map.c
    ** (KeyError) key :c not found in: %{2 => :b, :a => 1}

# Nested data structures
Often we will have maps inside maps, or even keywords lists inside maps, and so forth. 
Elixir provides conveniences for manipulating nested data structures via the put_in/2, update_in/2 and other macros

    # a keyword list of users where each value is a map containing the name, age and a list of programming languages each user likes.
    iex> users = [
    john: %{name: "John", age: 27, languages: ["Erlang", "Ruby", "Elixir"]},
    mary: %{name: "Mary", age: 29, languages: ["Elixir", "F#", "Clojure"]}
    ]
    # to access the age for john
    iex> users[:john].age
    27
    
we can also use this same syntax for updating the value:

    iex> users = put_in users[:john].age, 31
    [
    john: %{age: 31, languages: ["Erlang", "Ruby", "Elixir"], name: "John"},
    mary: %{age: 29, languages: ["Elixir", "F#", "Clojure"], name: "Mary"}
    ]

The update_in/2 macro is similar but allows us to pass a function that controls how the value changes.

    # let’s remove “Clojure” from Mary’s list of languages:
    iex> users = update_in users[:mary].languages, fn languages -> List.delete(languages, "Clojure") end
    [
    john: %{age: 31, languages: ["Erlang", "Ruby", "Elixir"], name: "John"},
    mary: %{age: 29, languages: ["Elixir", "F#"], name: "Mary"}
    ]
