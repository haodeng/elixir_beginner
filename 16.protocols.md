Protocols are a mechanism to achieve polymorphism in Elixir when you want behavior to vary depending on the data type. 

    defprotocol Utility do
      @spec type(t) :: String.t()
      def type(value)
    end

    # dispatching on a protocol is available to any data type that has implemented 
    # the protocol and a protocol can be implemented by anyone, at any time
    defimpl Utility, for: BitString do
      def type(_value), do: "string"
    end

    defimpl Utility, for: Integer do
      def type(_value), do: "integer"
    end

    IO.puts(Utility.type("foo")) # "string"
    IO.puts(Utility.type(123)) # "integer"

Another example:

    defprotocol Size do
      @doc "Calculates the size (and not the length!) of a data structure"
      def size(data)
    end

    defimpl Size, for: BitString do
      def size(string), do: byte_size(string)
    end

    defimpl Size, for: Map do
      def size(map), do: map_size(map)
    end

    defimpl Size, for: Tuple do
      def size(tuple), do: tuple_size(tuple)
    end

    IO.puts(Size.size("foo")) # 3
    IO.puts(Size.size({:ok, "hello"})) # 2
    IO.puts(Size.size(%{label: "some label"})) # 1
    # Size.size([1, 2, 3])  #** (Protocol.UndefinedError) protocol Size not implemented for [1, 2, 3]
    
It’s possible to implement protocols for all Elixir data types:

* Atom
* BitString
* Float
* Function
* Integer
* List
* Map
* PID
* Port
* Reference
* Tuple

# Protocols and structs
The power of Elixir’s extensibility comes when protocols and structs are used together.
MapSet is a struct

    set = %MapSet{} = MapSet.new
    Size.size(set) # ** (Protocol.UndefinedError) protocol Size not implemented for #MapSet<[]> of type MapSet (a struct)

Instead of sharing protocol implementation with maps, structs require their own protocol implementation.

    defimpl Size, for: MapSet do
      def size(set), do: MapSet.size(set)
    end

    IO.puts(Size.size(set)) # 0

you could come up with your own semantics for the size of your struct.

    defmodule User do
      defstruct [:name, :age]
    end

    defimpl Size, for: User do
      def size(_user), do: 2
    end

# Implementing Any
Manually implementing protocols for all types can quickly become repetitive and tedious. 
In such cases, Elixir provides two options: we can explicitly derive the protocol implementation for our types or automatically implement the protocol for all types. 
In both cases, we need to implement the protocol for Any.

## Deriving
Elixir allows us to derive a protocol implementation based on the Any implementation.

    defimpl Size, for: Any do
        def size(_), do: 0
    end
    
    # When deriving, Elixir will implement the Size protocol for OtherUser based on the implementation provided for Any
    defmodule OtherUser do
        # in order to use such implementation we would need to tell our struct to explicitly derive the Size protocol:
        @derive [Size]
        defstruct [:name, :age]
    end
    
## Fallback to Any
Another alternative to @derive is to explicitly tell the protocol to fallback to Any when an implementation cannot be found. 

    defprotocol Size do
        @fallback_to_any true
        def size(data)
    end
    
    defimpl Size, for: Any do
        def size(_), do: 0
    end
all data types (including structs) that have not implemented the Size protocol will be considered to have a size of 0
