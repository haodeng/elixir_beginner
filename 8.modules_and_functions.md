# Modules and functions

## Compilation

    # file named math.ex
    defmodule Math do
      def sum(a, b) do
        a + b
      end
    end
This file can be compiled using elixirc

    elixirc math.ex
This will generate a file named Elixir.Math.beam containing the bytecode for the defined module. If we start iex again, our module definition will be available

    iex> Math.sum(1, 2)
    3

Elixir projects are usually organized into three directories:

* _build - contains compilation artifacts
* lib - contains Elixir code (usually .ex files)
* test - contains tests (usually .exs files)


When working on actual projects, the build tool called mix will be responsible for compiling and setting up the proper paths for you.

## Scripted mode
In addition to the Elixir file extension .ex, Elixir also supports .exs files for scripting. 
Elixir treats both files exactly the same way, the only difference is in intention. 
.ex files are meant to be compiled while .exs files are used for scripting. 

    # a file called math.exs
    defmodule Math do
      def sum(a, b) do
        a + b
      end
    end

    IO.puts Math.sum(1, 2)
And execute it as:

    elixir math.exs
the module was compiled and loaded into memory, but no .beam file was written to disk.

## Named functions
Inside a module, we can define functions with def/2 and private functions with defp/2. 

    defmodule Math do
      def sum(a, b) do
        do_sum(a, b)
      end

      # private function
      defp do_sum(a, b) do
        a + b
      end
    end

    IO.puts Math.sum(1, 2)    #=> 3
    # a private function can only be invoked locally.
    IO.puts Math.do_sum(1, 2) #=> ** (UndefinedFunctionError)

Function declarations also support guards and multiple clauses. If a function has several clauses, Elixir will try each clause until it finds one that matches.

    defmodule Math do
      # The trailing question mark in zero? means that this function returns a boolean
      def zero?(0) do
        true
      end

      def zero?(x) when is_integer(x) do
        false
      end
    end

    IO.puts Math.zero?(0)         #=> true
    IO.puts Math.zero?(1)         #=> false
    # Giving an argument that does not match any of the clauses raises an error
    IO.puts Math.zero?([1, 2, 3]) #=> ** (FunctionClauseError)
    IO.puts Math.zero?(0.0)       #=> ** (FunctionClauseError)
    
Similar to constructs like if, named functions support both do: and do/end block syntax
For example, we can edit math.exs to look like this:

    defmodule Math do
      def zero?(0), do: true
      def zero?(x) when is_integer(x), do: false
    end
    
## Function capturing
The capture operator bridges this gap by allowing named functions to be assigned to variables and passed as arguments in the same way we assign, invoke and pass anonymous functions.

    iex> Math.zero?(0)
    true
    iex> fun = &Math.zero?/1
    &Math.zero?/1
    iex> is_function(fun)
    true
    # Elixir makes a distinction between anonymous functions and named functions, where the former must be invoked with a dot (.)
    iex> fun.(0)
    true

the capture syntax can also be used as a shortcut for creating functions:
The &1 represents the first argument passed into the function.

    # the same as fn x -> x + 1 end
    iex> fun = &(&1 + 1)
    #Function<6.71889879/1 in :erl_eval.expr/5>
    iex> fun.(1)
    2

    iex> fun2 = &"Good #{&1}"
    #Function<6.127694169/1 in :erl_eval.expr/5>
    iex)> fun2.("morning")
    "Good morning"
    
## Default arguments

    defmodule Concat do
      def join(a, b, sep \\ " ") do
        a <> sep <> b
      end
    end

    IO.puts Concat.join("Hello", "world")      #=> Hello world
    IO.puts Concat.join("Hello", "world", "_") #=> Hello_world
    
If a function with default values has multiple clauses, it is required to create a function head (a function definition without a body) for declaring defaults

    defmodule Concat do
      # A function head declaring defaults
      def join(a, b \\ nil, sep \\ " ")

      # The leading underscore in _sep means that the variable will be ignored in this function;
      def join(a, b, _sep) when is_nil(b) do
        a
      end

      def join(a, b, sep) do
        a <> sep <> b
      end
    end

    IO.puts Concat.join("Hello", "world")      #=> Hello world
    IO.puts Concat.join("Hello", "world", "_") #=> Hello_world
    IO.puts Concat.join("Hello")               #=> Hello
    
