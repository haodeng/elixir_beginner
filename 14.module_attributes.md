Module attributes in Elixir serve three purposes:

* annotate the module, often with information to be used by the user or the VM.
* constants.
* a temporary module storage to be used during compilation.

# As annotations
we are defining the module documentation by using the module attribute syntax. 

    # filename: math.ex
    defmodule Math do
      @moduledoc """
      Provides math-related functions.

      ## Examples

        iex> Math.sum(1, 2)
        3

      """

      @doc """
      Calculates the sum of two numbers.
      """
      def sum(a, b), do: a + b
    end
Elixir has a handful of reserved attributes.
* @moduledoc - provides documentation for the current module.
* @doc - provides documentation for the function or macro that follows the attribute.
* @spec - provides a typespec for the function that follows the attribute.
* @behaviour - (notice the British spelling) used for specifying an OTP or user-defined behaviour.

      $ elixirc math.ex
      $ iex
      iex> h Math # Access the docs for the module Math
      ...
      iex> h Math.sum # Access the docs for the sum function
      ...
      
a tool called ExDoc which is used to generate HTML pages from the documentation.

# As “constants”

    defmodule MyServer do
        @initial_state %{host: "127.0.0.1", port: 3456}
        IO.inspect @initial_state
        
        # Trying to access an attribute that was not defined will print a warning
        # warning: undefined module attribute @unknown, please remove access to @unknown or explicitly set it before access
        @unknown
        
        @my_data 14
        def first_data, do: @my_data
        @my_data 13
        def second_data, do: @my_data
    end
    
    MyServer.first_data #=> 14
    MyServer.second_data #=> 13

Functions may be called when defining a module attribute:

    defmodule MyApp.Status do
        @service URI.parse("https://example.com")
        def status(email) do
            SomeHttpClient.get(@service)
        end
    end
The function above will be called at compilation time and its return value, not the function call itself, is what will be substituted in for the attribute.
So the above will effectively compile to this:

    defmodule MyApp.Status do
        def status(email) do
            SomeHttpClient.get(%URI{
            authority: "example.com",
            host: "example.com",
            port: 443,
            scheme: "https"
            })
        end
    end

This can be useful for pre-computing constant values, but it can also cause problems if you’re expecting the function to be called at runtime. 
Every time an attribute is read inside a function, Elixir takes a snapshot of its current value. Therefore if you read the same attribute multiple times inside multiple functions, you may end-up making multiple copies of it. 
 if you are using functions to compute large module attributes, that can slow down compilation.

    # instead of this:
    def some_function, do: do_something_with(@example)
    def another_function, do: do_something_else_with(@example)
    
    # Prefer this:
    def some_function, do: do_something_with(example())
    def another_function, do: do_something_else_with(example())
    defp example, do: @example

# Accumulating attributes
Normally, repeating a module attribute will cause its value to be reassigned, but there are circumstances where you may want to configure the module attribute so that its values are accumulated

    defmodule Foo do
        Module.register_attribute __MODULE__, :param, accumulate: true

        @param :foo
        @param :bar
        # here @param == [:bar, :foo]
    end
