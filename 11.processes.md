Elixir’s processes should not be confused with operating system processes. 
Processes in Elixir are extremely lightweight in terms of memory and CPU (even compared to threads as used in many other programming languages). 

# spawn
spawn/1 takes a function which it will execute in another process.

    # returns a PID (process identifier)
    iex> pid = spawn(fn -> 1 + 2 end)
    #PID<0.43.0>
    
    # At this point, the process you spawned is very likely dead. 
    # The spawned process will execute the given function and exit after the function is done
    iex> Process.alive?(pid)
    false

We can retrieve the PID of the current process by calling self/0

    iex> self()
    #PID<0.41.0>
    iex> Process.alive?(self())
    true

# send and receive
send messages to a process with send/2 and receive them with receive/1:

    # When a message is sent to a process, the message is stored in the process mailbox.
    iex> send(self(), {:hello, "world"})
    {:hello, "world"}
    iex> receive do
    ...>   {:hello, msg} -> msg
    ...>   {:world, _msg} -> "won't match"
    ...> end
    "world"
  
 The process that sends the message does not block on send/2, it puts the message in the recipient’s mailbox and continues.
 The receive/1 block goes through the current process mailbox searching for a message that matches any of the given patterns.
 If there is no message in the mailbox matching any of the patterns, the current process will wait until a matching message arrives. 
 
     iex> receive do
    ...>   {:hello, msg}  -> msg
        # A timeout can also be specified
    ...> after
    ...>   1_000 -> "nothing after 1s"
    ...> end
    "nothing after 1s"
    
    iex> parent = self()
    #PID<0.41.0>
    iex> spawn(fn -> send(parent, {:hello, self()}) end)
    #PID<0.48.0>
    iex> receive do
    ...>   {:hello, pid} -> "Got hello from #{inspect pid}"
    ...> end
    "Got hello from #PID<0.48.0>"

While in the shell, you may find the helper flush/0 quite useful. It flushes and prints all the messages in the mailbox.

    iex> send(self(), :hello)
    :hello
    iex> flush()
    :hello
    :ok

# Links
 Elixir processes are isolated and don’t share anything by default. 
 Therefore, a failure in a process will never crash or corrupt the state of another process. 
 Links, however, allow processes to establish a relationship in case of failure. 
 We often link our processes to supervisors which will detect when a process dies and start a new process in its place.
 
     iex> spawn(fn -> raise "oops" end)
    #PID<0.58.0>

    [error] Process #PID<0.58.00> raised an exception
    ** (RuntimeError) oops
        (stdlib) erl_eval.erl:668: :erl_eval.do_apply/6
It merely logged an error but the parent process is still running. That’s because processes are isolated. If we want the failure in one process to propagate to another one, we should link them. This can be done with spawn_link/1

    iex> self()
    #PID<0.41.0>
    iex> spawn_link(fn -> raise "oops" end)

    ** (EXIT from #PID<0.41.0>) evaluator process exited with reason: an exception was raised:
        ** (RuntimeError) oops
            (stdlib) erl_eval.erl:668: :erl_eval.do_apply/6

    [error] Process #PID<0.289.0> raised an exception
    ** (RuntimeError) oops
        (stdlib) erl_eval.erl:668: :erl_eval.do_apply/6
we now see a message saying the parent process, which is the shell process, has received an EXIT signal from another process causing the shell to terminate.

While other languages would require us to catch/handle exceptions, in Elixir we are actually fine with letting processes fail because we expect supervisors to properly restart our systems. “Failing fast” is a common philosophy when writing Elixir software!

# Tasks
Tasks build on top of the spawn functions to provide better error reports and introspection

    iex(1)> Task.start(fn -> raise "oops" end)
    {:ok, #PID<0.55.0>}

    15:22:33.046 [error] Task #PID<0.55.0> started from #PID<0.53.0> terminating
    ** (RuntimeError) oops
        (stdlib) erl_eval.erl:668: :erl_eval.do_apply/6
        (elixir) lib/task/supervised.ex:85: Task.Supervised.do_apply/2
        (stdlib) proc_lib.erl:247: :proc_lib.init_p_do_apply/3
    Function: #Function<20.99386804/0 in :erl_eval.expr/5>
        Args: []
Instead of spawn/1 and spawn_link/1, we use Task.start/1 and Task.start_link/1 which return {:ok, pid} rather than just the PID. 

# State
Using processes to maintain state and name registration are very common patterns in Elixir applications.
Elixir provides agents, which are simple abstractions around state:

    iex> {:ok, pid} = Agent.start_link(fn -> %{} end)
    {:ok, #PID<0.72.0>}
    iex> Agent.update(pid, fn map -> Map.put(map, :hello, :world) end)
    :ok
    iex> Agent.get(pid, fn map -> Map.get(map, :hello) end)
    :world
